plugins {
    id 'java'
    id 'java-library'
    id 'maven-publish'
}
dependencies {
    implementation project(":FIRO_TSEnsembles");
}
configurations {
    windows_x64
    linux_x64
}

group 'hec.dss.ensemble'

repositories {
    mavenCentral()
    maven {
        url = 'https://www.hec.usace.army.mil/nexus/repository/maven-public'
    }
}
dependencies {
    implementation project(":FIRO_TSEnsembles")
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.2'
    implementation ('mil.army.usace.hec:hec-monolith:3.3.23'){
        exclude group: "codebase", module: "*"
        exclude group: "com.sun.media", module: "*"
        exclude group: "javax.media", module: "*"
    }
}

test {
    useJUnitPlatform()
    jvmArgs = ["-Djava.library.path=${buildDir}/x64"]
}
dependencies {
    windows_x64 'mil.army.usace.hec:javaHeclib:7-IO-RC1-win-x86_64@zip'
    linux_x64 'mil.army.usace.hec:javaHeclib:7-IO-RC1-linux-x86_64@zip'
}

task copyNatives(type: Copy){
    if (org.gradle.internal.os.OperatingSystem.current().isWindows()) {
        configurations.windows_x64.asFileTree.each {
            from(zipTree(it))
            into "$buildDir/x64"
        }
    }
    if (org.gradle.internal.os.OperatingSystem.current().isLinux()) {
        configurations.linux_x64.asFileTree.each {
            from(zipTree(it))
            into "$buildDir/x64"
        }
    }
}
jar {
    dependsOn copyNatives
}

def getGitTag() {
    def stdout = new ByteArrayOutputStream()

    try {
        exec {
            commandLine 'git', 'tag', '--list', '--contains', 'HEAD'
            standardOutput = stdout
        }
        return stdout.toString().trim().split('\n')
    } catch (Exception e) {
        return null
    }
}

publishing {
    def gitTags = getGitTag()

    gitTags.each { gitTag ->
        if (gitTag.contains("dss-")) {
            def verNum = gitTag.trim().split('-')
            version = verNum[1]
        }
    }
    if(version.toString().contains("unspecified")) {
        version = "SNAPSHOT"
    }

    publications {
        mavenJava(MavenPublication) {
            from components.java
            groupId = 'mil.army.usace.hec'
            artifactId = 'dss-ensembles'
        }
    }
    repositories {
        maven {
            name = "hecNexus"
            credentials {
                username = System.getenv("MAVEN_USERNAME")
                password = System.getenv("MAVEN_PASSWORD")
            }
            def releasesRepoUrl = "https://www.hec.usace.army.mil/nexus/repository/maven-releases/"
            def snapshotsRepoUrl = "https://www.hec.usace.army.mil/nexus/repository/maven-snapshots/"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
        }
    }
}

publish.dependsOn(build)

build {
    dependsOn jar
}
