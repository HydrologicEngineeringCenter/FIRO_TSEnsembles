import org.gradle.internal.os.OperatingSystem

buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "gradle.plugin.com.palantir.gradle.gitversion:gradle-git-version:0.12.0-rc2"
    }
}

plugins {
    id 'java'
    id 'maven-publish'
    id "com.palantir.git-version" version "3.0.0"
}

repositories {
    mavenCentral()
}

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

dependencies {
    implementation 'org.apache.directory.studio:org.apache.commons.lang:2.6'
    runtimeOnly 'org.xerial:sqlite-jdbc:3.40.1.0'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.9.2'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher:1.9.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.2'
    testRuntimeOnly 'org.junit.vintage:junit-vintage-engine:5.9.2'
    implementation 'org.jdom:jdom:1.1'
    implementation 'commons-lang:commons-lang:2.6'
    implementation 'javax.measure:unit-api:2.1.3'
    implementation 'tech.units:indriya:2.2'
    implementation 'systems.uom:systems-common:2.1'
}

test {
     useJUnitPlatform()
    if (OperatingSystem.current().isWindows()) {
        jvmArgs = ["-Djava.io.tmpdir=c:/temp"]
     }
}

jar {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

task showGit () {
    doFirst {
        def gitInfo = versionDetails()
        println( "Used:    " + getVersion() ) // The select version (the project.version field)
        println( "Tag:     " + gitInfo.lastTag) // actual tag or commit hash(short) if no tag
        println( "Hash:    " + gitInfo.gitHash) // short commit hash
        println( "Branch:  " + gitInfo.branchName) // branch name (null if tag checkout)
        println( "TagBuild:" + gitInfo.isCleanTag) // true if repoi is in dettached head mode(e.g. git checkout <tag>)
    }
}

def versionLabel(gitInfo) {
    def branch = gitInfo.branchName // all branches are snapshots, only tags get released
    def verNum = "-SNAPSHOT"
    def tag = gitInfo.lastTag
    println(tag)
    if (tag.contains("ts-")) {
        def tagArray = tag.trim().split('-')
        verNum = tagArray[1]
    }
    return branch == null ? verNum : branch + verNum
}

publishing {
    version = versionLabel(versionDetails())
    println("selected tag: " + version)
    publications {
        mavenJava(MavenPublication) {
            from components.java
            groupId = 'mil.army.usace.hec'
            artifactId = 'ts-ensembles'
        }
    }
    repositories {
        maven {
            name = "hecNexus"
            credentials {
                username = System.getenv("MAVEN_USERNAME")
                password = System.getenv("MAVEN_PASSWORD")
            }
            def releasesRepoUrl = "https://www.hec.usace.army.mil/nexus/repository/maven-releases/"
            def snapshotsRepoUrl = "https://www.hec.usace.army.mil/nexus/repository/maven-snapshots/"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
        }
    }
}

publish.dependsOn(build)
build {finalizedBy javadoc }

