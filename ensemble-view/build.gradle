buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "gradle.plugin.com.palantir.gradle.gitversion:gradle-git-version:0.12.0-rc2"
    }
}

plugins {
    id 'java'
    id 'application'
    id 'maven-publish'
    id "com.palantir.git-version" version "3.0.0"
}

repositories {
    mavenCentral()
    maven {
        url = 'https://www.hec.usace.army.mil/nexus/repository/maven-public'
    }
}

dependencies {
    implementation project(":FIRO_TSEnsembles")
    implementation project(":ensemble-dss")
    implementation ('mil.army.usace.hec:hec-monolith:3.3.23') {
        transitive = false
    }
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.2'
}

test {
    useJUnitPlatform()
}


application {
    mainClassName = 'hec.ensembleview.EnsembleViewer'
}

jar {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
    manifest {
        attributes 'Main-Class' : 'hec.ensembleview.EnsembleViewer'
    }
}

task showGit () {
    doFirst {
        def gitInfo = versionDetails()
        println( "Used:    " + getVersion() ) // The select version (the project.version field)
        println( "Tag:     " + gitInfo.lastTag) // actual tag or commit hash(short) if no tag
        println( "Hash:    " + gitInfo.gitHash) // short commit hash
        println( "Branch:  " + gitInfo.branchName) // branch name (null if tag checkout)
        println( "TagBuild:" + gitInfo.isCleanTag) // true if repoi is in dettached head mode(e.g. git checkout <tag>)
    }
}

def versionLabel(gitInfo) {
    def verNum = "-SNAPSHOT"
    def tag = gitInfo.lastTag
    println(tag)
    if (tag.contains("ev-")) {
        def tagArray = tag.trim().split('-')
        verNum = tagArray[1]
    }
    return verNum
}


publishing {
    version = versionLabel(versionDetails())
    println("selected tag: " + version)
    publications {
        mavenJava(MavenPublication) {
            from components.java
            groupId = 'mil.army.usace.hec'
            artifactId = 'ensemble-view'
        }
    }
    repositories {
        maven {
            name = "hecNexus"
            credentials {
                username = System.getenv("MAVEN_USERNAME")
                password = System.getenv("MAVEN_PASSWORD")
            }
            def releasesRepoUrl = "https://www.hec.usace.army.mil/nexus/repository/maven-releases/"
            def snapshotsRepoUrl = "https://www.hec.usace.army.mil/nexus/repository/maven-snapshots/"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
        }
    }
}

publish.dependsOn(build)